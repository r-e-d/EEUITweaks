/*******************************************************************************
 * Replace the dragon button in the official SoD interface
 ******************************************************************************/
COPY_EXISTING ~GUILS10.bam~ ~override~
	LPF FIND_FREE_PVRZ_INDEX RET free_index END

	READ_LONG 0x14 frame_base_off
	READ_LONG 0x18 cycle_base_off
	READ_LONG 0x1c data_base_off

	READ_SHORT ( cycle_base_off + 0x00 ) frame_cnt
	READ_SHORT ( cycle_base_off + 0x02 ) frame_off

	FOR ( i = 0; i < frame_cnt; ++i ) BEGIN
		SET offset = frame_base_off + 0x0c * (frame_off + i)

		READ_SHORT ( offset + 0x02 ) height
		READ_SHORT ( offset + 0x08 ) data_off
		READ_SHORT ( offset + 0x0a ) count

		SET src_x = 0
		SET src_y = height * i

		FOR ( j = 0; j < count; ++j ) BEGIN
			offset = data_base_off + 0x1c * (data_off + j)

			WRITE_LONG ( offset + 0x00 ) free_index
			WRITE_LONG ( offset + 0x04 ) src_x
			WRITE_LONG ( offset + 0x08 ) src_y

			READ_LONG ( offset + 0x0c ) block_width
			src_x = src_x + block_width
		END
	END

COPY ~%PKGNAME%/copy/ReplaceDragon/MOS1000.PVRZ~ ~override/MOS%free_index%.PVRZ~
